apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: "com.google.gms.google-services"

def build_param = "${build}"
// Needed for CI
if (build_param != "dev") {
    //exclude production build
    android.variantFilter { variant ->
        if (variant.buildType.name == 'dev') {
            variant.setIgnore(true)
        }
    }
} else {
    //exclude all except production build
    android.variantFilter { variant ->
        if (variant.buildType.name != 'dev') {
            variant.setIgnore(true)
        }
    }
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.yashovardhan99.healersdiary"
        minSdkVersion 21
        targetSdkVersion 30
        def major = 2
        def minor = 0
        def patch = 0
        def type = 'alpha'
        def build = 0
        def types = ['alpha': 0, 'beta': 1, 'rc': 2, 'release': 3]
        def curType = types.get(type, 3)
        def standard = ((major * 100) + minor) * 100 + patch
        versionCode = (standard * 10 + curType) * 100 + build
        if (type == "release") versionName = "$major.$minor.$patch"
        else if (build == 0) versionName = "$major.$minor.$patch-$type"
        else if (build < 10) versionName = "$major.$minor.$patch-${type}0${build}"
        else versionName = "$major.$minor.$patch-$type$build"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        dev { //needed for CI
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    lintOptions {
        abortOnError false
    }
    dynamicFeatures = [':online']
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation platform('com.google.firebase:firebase-bom:26.4.0')
    api 'com.google.firebase:firebase-analytics-ktx'
    api 'androidx.multidex:multidex:2.0.1'
    api 'com.google.firebase:firebase-crashlytics-ktx'
    api 'com.google.firebase:firebase-auth-ktx'
    api 'com.google.android.gms:play-services-auth:19.0.0'
    api 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    api "androidx.core:core-ktx:1.3.2"
    api 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    def room_version = "2.3.0-beta01"
    api "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    api "androidx.room:room-ktx:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"
    def lifecycle_version = "2.2.0"
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    def nav_version = "2.3.3"
    api "androidx.navigation:navigation-fragment-ktx:$nav_version"
    api "androidx.navigation:navigation-ui-ktx:$nav_version"
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
    api "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"
    def paging_version = "3.0.0-alpha13"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    testImplementation "androidx.paging:paging-common-ktx:$paging_version"
    api "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    kapt 'androidx.hilt:hilt-compiler:1.0.0-alpha03'
    api "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    api "androidx.datastore:datastore-preferences:1.0.0-alpha06"
    api 'com.jakewharton.timber:timber:4.7.1'
    api 'androidx.activity:activity-ktx:1.2.0-rc01'
    api 'androidx.fragment:fragment-ktx:1.3.0-rc02'
}